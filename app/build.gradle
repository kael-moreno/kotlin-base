apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

android {
    compileSdk rootProject.ext.compileSdkVersion

    defaultConfig {
        applicationId "com.coreproc.kotlin.kotlinbase"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode code
        versionName "${versionMajor}.${versionMinor}.${versionPatch}+${code}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'SENTRY_DSN', SENTRY_DSN
        flavorDimensions += "default"
    }

    buildFeatures {
        viewBinding true
    }

    //Hilt
    kapt {
        correctErrorTypes = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    //

    signingConfigs {
        release {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
        }
        release {
            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {

        /**
         * Internal Development
         * */
        internal {
            resValue "string", "app_name_variant", "[INTERNAL] ${APP_NAME}"
            versionName "${versionMajor}.${versionMinor}.${versionPatch}+${code}"
            setProperty("archivesBaseName", "kotlin-base-" + versionName)
            buildConfigField 'String', 'HOST', BASE_URL_INTERNAL
        }

        /**
         * Staging
         * */
        staging {
            resValue "string", "app_name_variant", "[STAGING] ${APP_NAME}"
            versionName "${versionMajor}.${versionMinor}.${versionPatch}+${code}"
            setProperty("archivesBaseName", "kotlin-base-" + versionName)
            buildConfigField 'String', 'HOST', BASE_URL_STAGING
        }

        /**
         * Play Store Release
         * */
        production {
            resValue "string", "app_name_variant", APP_NAME
            versionName "${versionMajor}.${versionMinor}.${versionPatch}+${code}"
            setProperty("archivesBaseName", "kotlin-base-" + versionName)
            buildConfigField 'String', 'HOST', BASE_URL_PROD
        }
    }
    namespace 'com.coreproc.kotlin.kotlinbase'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.10"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    // App's dependencies
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.supportLibraryVersion"
    implementation "androidx.fragment:fragment-ktx:$rootProject.fragmentKtxVersion"
    implementation "androidx.activity:activity-ktx:$rootProject.activityKtxVersion"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-common:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$rootProject.lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycleVersion"

    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation 'android.arch.paging:runtime:1.0.1'
    kapt "android.arch.lifecycle:common-java8:1.1.1"

    // Hilt Dependencies
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"
    kapt "com.google.dagger:hilt-android-compiler:$rootProject.hiltVersion"

    // Apache Common
    implementation "org.apache.commons:commons-lang3:$rootProject.apacheVersion"

    // Network
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$rootProject.okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpVersion"
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // Glide 4
    implementation "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    kapt "com.github.bumptech.glide:compiler:$rootProject.glideVersion"

    // Sentry
    implementation "io.sentry:sentry-android:$rootProject.sentryVersion"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.coroutinesVersion"

    // Timber
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"

}
